#!/usr/bin/env perl6

#This will send messages to Slack.

use WebService::Slack::Webhook;

my $url = $*HOME.add(".slack-url").slurp.chomp;
my $slack = WebService::Slack::Webhook.new(:$url);

multi sub MAIN($msg, :username(:$u), :icon(:$i)) {
	$slack.send(%(
		text       => $msg,
		username   => $u,
		icon_emoji => $i
	));
}


multi sub MAIN(:file(:$f)!, :username(:$u), :icon(:$i)) {
	$slack.send(%(
		text       => ' ```' ~ $f.IO.slurp ~ '```',
		username   => $u,
		icon_emoji => $i
	));
}


multi sub MAIN(:timeout(:$t), :username(:$u), :icon(:$i)) {
	my $stdin = $*IN.Supply(:size<100>);
	my $buf = "";

	react {
		whenever signal(SIGINT) {
			say "Killed after { now - INIT now}";
			exit;
		}

		whenever $stdin -> $msg {
			$buf ~= $msg;
			my @l = $buf.lines;

			if @l.elems > 1 {
				my $li = @l.end;
				@l[$li] ~= "\n" if $buf ~~ /\n$/;
				$slack.send(%(
					text       => ' ```' ~ @l[0..($li-1)].join("\n") ~ '```',
					username   => $u,
					icon_emoji => $i
				));
				$buf = @l[$li];
			}

			LAST {
				$slack.send(%(
					text       => ' ```' ~ $buf ~ '```',
					username   => $u,
					icon_emoji => $i
				));
				done;
			}
		}
	}
}
